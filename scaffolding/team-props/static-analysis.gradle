apply plugin: 'com.novoda.static-analysis'

staticAnalysis {
    penalty none

    checkstyle {
        toolVersion checkstyleVersion
        exclude project.fileTree('src/test/java')
        exclude project.fileTree('src/testDebug/java')
        configFile teamPropsFile('static-analysis/checkstyle-modules.xml')
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }

    pmd {
        toolVersion pmdVersion
        exclude project.fileTree('src/test/java')
        exclude project.fileTree('src/testDebug/java')

        ruleSetFiles = rootProject.files('team-props/static-analysis/pmd-rules.xml')
        ruleSets = []   // Note: this is a workaround to make the <exclude-pattern>s in pmd-rules.xml actually work
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }

    findbugs {
        toolVersion findbugsVersion
        excludeFilter rootProject.file('team-props/static-analysis/findbugs-excludes.xml')
        includeVariants { variant -> excludeTestAndRelease(variant) }
    }
    
    lintOptions {
        lintConfig rootProject.file('team-props/static-analysis/lint-config.xml')
        includeVariants { variant -> excludeTestAndRelease(variant) }
        ignoreTestSources true // See https://groups.google.com/forum/#!topic/lint-dev/RGTvK_uHQGQ
    }
    
    detekt {
        profile('main') {
            input = "$projectDir/src/main/java"
            config = rootProject.file('team-props/detekt.yml')
            filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
            output = "$projectDir/build/reports/detekt"
        }
    }

    ktlint {
        android true
        // Note: KtLint supports only minimal configuration via an .editorconfig file
        // in the project root folder. For an example:
        // https://github.com/squanchy-dev/squanchy-android/blob/develop/.editorconfig
    }
}

static boolean excludeTestAndRelease(variant) {
    return !variant.name.toLowerCase().endsWith('test') && !variant.name.toLowerCase().endsWith('release')
}
